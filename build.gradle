import java.nio.file.Files
import java.nio.file.NoSuchFileException
import java.nio.file.Paths
import java.time.LocalDateTime

/*
 * Copyright 2016 E-System LLC
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.0.0.2929"
    }
}
plugins {
    id "com.github.ben-manes.versions" version "0.44.0"
}
wrapper {
    gradleVersion = '8.0.2'
}

apply from: "build-${profile}.gradle"
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
        cacheDynamicVersionsFor 0, 'seconds'
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

group = ext.mainGroup
version = ext.mainVersion + (ext.snapshot ? '-SNAPSHOT' : '')

repositories {
    mavenCentral()
    maven {
        url "${artifactory_contextUrl}/libs-${project.ext.snapshot ? 'all-local' : 'release'}"
        credentials {
            username = 'public-reader'
            password = 'Public-reader_123'
        }
    }
    mavenLocal()
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = JavaVersion.VERSION_1_8
    }
    module {
        downloadJavadoc = false
        downloadSources = false
        inheritOutputDirs = true
        excludeDirs += file('gradle')
        excludeDirs += file('out')
    }
}

def propName = rootProject.name
def fullPath = '/com/es/lib/spring'
if (!fullPath.startsWith('/src/main/resources')) {
    fullPath = '/src/main/resources' + fullPath
}
def versionString = "${project.version}"
def filePath = "${project.projectDir}${fullPath}/"
new File(filePath).mkdirs()
try {
    Files.delete(Paths.get(filePath, 'build.properties'))
} catch (NoSuchFileException ignore) {
}
println("Create file for property: ${filePath}")
project.ant.propertyfile(file: "${filePath}build.properties") {
    ant.entry(key: 'name', value: propName)
    ant.entry(key: 'version', value: versionString)
    ant.entry(key: 'date', value: "${LocalDateTime.now().toString()}")
}

jar {
    manifest {
        attributes(
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'E-SYSTEM'
        )
    }
}
javadoc {
    failOnError = false
    options.encoding = 'utf-8'
}
java{
    withSourcesJar()
    withJavadocJar()
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
ext {
    springVersion = '2.7.8'
    springSecurityVersion = '5.7.6'
    springSecurityOAuth = '2.5.2.RELEASE'
    pgVersion = '42.5.4'
    lombokVersion = '1.18.24'
    springDocVersion = '1.6.14'
    commonVersion = "1.5.0${project.ext.snapshot ? '-SNAPSHOT' : ''}"
}
dependencies {
    api "com.es.lib:common-lib:${commonVersion}"
    api "com.es.lib:common-entity:${commonVersion}"
    api "com.es.lib:common-dto:${commonVersion}"
    api "com.es.lib:common-validator:${commonVersion}"

    api "commons-io:commons-io:2.11.0"
    api "javax.servlet:javax.servlet-api:4.0.1"
    api "net.coobird:thumbnailator:0.4.19"

    compileOnly "org.springframework.boot:spring-boot-starter-web:${springVersion}"
    compileOnly "org.springframework.boot:spring-boot-starter-jdbc:${springVersion}"
    compileOnly "org.springframework.boot:spring-boot-starter-security:${springVersion}"
    compileOnly "org.springframework.boot:spring-boot-starter-data-jpa:${springVersion}"
    compileOnly "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOAuth}"
    compileOnly "org.springdoc:springdoc-openapi-ui:${springDocVersion}"
    compileOnly "org.postgresql:postgresql:${pgVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    testImplementation "org.springframework.boot:spring-boot-starter-web:${springVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-security:${springVersion}"
    testImplementation "org.springframework.security:spring-security-test:${springSecurityVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-security:${springVersion}"
    testImplementation "org.springframework.data:spring-data-commons:${springVersion}"
    testImplementation "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOAuth}"
    testImplementation "org.springframework.security:spring-security-jwt:1.1.1.RELEASE"
    testImplementation "org.springdoc:springdoc-openapi-ui:${springDocVersion}"
}

test {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
    finalizedBy jacocoTestReport
}

def sonarAvailable = properties['sonar_url'] != null && properties['sonar_user'] != null && properties['sonar_password'] != null
if (sonarAvailable) {
    apply plugin: 'org.sonarqube'
    sonar {
        properties {
            property "sonar.host.url", "${sonar_url}"
            property "sonar.login", "${sonar_user}"
            property "sonar.password", "${sonar_password}"
        }
    }
}